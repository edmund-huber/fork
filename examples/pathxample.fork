#  libfork, a base library for the Fork language
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2015
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.


import err
import fs
import proc
import tty


func pad(p uintptr)
  mut i uintptr = 0

  while i < p
    tty:out(" ")

    i++
  /while
/func


func dirDump(dir ptr fs:Dir, p uintptr) ptr err:Error
  tty:out(dir'name)
  tty:outln(" {")

  while true
    mut {file,err} = dir.next()
    if err?
      return err
    /if

    if not file?
      break
    /if

    pad(p + 2)
    if file.isDir()
      {dir,err} = file.openDir()

      fs:fileFree(file)

      if err?
        return err
      /if

      mut err = dirDump(dir, p + 2)
      fs:dirClose(dir)

      if err?
        return err
      /if
    else
      tty:outln(file'name)
      fs:fileFree(file)
    /if
  /while

  pad(p)
  tty:outln("}")

  return null
/func


entry
  mut arg = proc:args()

  if arg'len < 1
    tty:failln("No arguments given")
  /if

  mut {file,err} = fs:stat(arg'args[0])
  if err?
    tty:failln(err'msg)
  /if

  if not file.isDir()
    tty:failln("Argument is not a directory")
  /if

  mut {dir,err} = file.openDir()
  fs:fileFree(file)

  if err?
    tty:failln(err'msg)
  /if

  err = dirDump(dir, 0)

  if err?
    tty:failln(err'msg)
  /if

  fs:dirClose(dir)
/entry
