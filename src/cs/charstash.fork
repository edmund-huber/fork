#  libfork, a base library for the Fork language
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2014, 2015
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.


module cs


import buffer
import cs
import mem
import txt


# charstash init/deinit functions

func free(cs ptr cs:Charstash)
  if cs?
    buffer:deinit(ptr cs'buf)
    mem:free(cs)
  /if
/func


func new(cap uintptr) ptr cs:Charstash
  mut cs = <ptr cs:Charstash>(mem:zalloc(size(cs:Charstash)))

  buffer:init(ptr cs'buf, cap)

  return cs
/func


func unwrap(cs ptr cs:Charstash) ptr uint8
  cs.addChar(0)

  mut ret = cs'buf'base
  mem:free(cs)

  return ret
/func


# charstash methods

method cs:Charstash.add(str ptr uint8) ptr cs:Charstash
  me'buf.add(str, txt:strlen(str))

  return me
/method


method cs:Charstash.addChar(ch uint8) ptr cs:Charstash
  me'buf.addByte(ch)

  return me
/method


method cs:Charstash.addInt(num uintptr) ptr cs:Charstash
  mut buf [20] int8

  txt:numtostr(num, ptr buf[0], 20)

  me.add(ptr buf[0])

  return me
/method


method cs:Charstash.addLine(str ptr uint8) ptr cs:Charstash
  me.add(str)
  me.add("\n")

  return me
/method


method cs:Charstash.addWord(str ptr uint8) ptr cs:Charstash
  me.add(str)
  me.addChar(32)

  return me
/method


method cs:Charstash.len() uintptr
  return me'buf.len()
/method


method cs:Charstash.prepend(str ptr uint8) ptr cs:Charstash
  me'buf.prepend(str, txt:strlen(str))

  return me
/method


method cs:Charstash.prependChar(ch uint8) ptr cs:Charstash
  me'buf.prependByte(ch)

  return me
/method


method cs:Charstash.prependInt(num uintptr) ptr cs:Charstash
  mut buf [20] int8

  txt:numtostr(num, ptr buf[0], 20)

  me.prepend(ptr buf[0])

  return me
/method


method cs:Charstash.prependLine(str ptr uint8) ptr cs:Charstash
  me.prepend(str)
  me.prepend("\n")

  return me
/method


method cs:Charstash.prependWord(str ptr uint8) ptr cs:Charstash
  me.prepend(str)
  me.prependChar(32)

  return me
/method
