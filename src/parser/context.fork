#  libfork, a base library for the Fork language
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2015
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.


import ast
import astfn
import file
import list
import map
import mem
import parser
import proc
import sema
import synt
import text
import utils


func findImport(ctx ptr parser:Context, importName ptr ast:StringDef, importAst ptr ptr ast:PNode) ptr list:List
  mut fordName = text:strappend(importName'name, ".ford")

  mut len = list:listLen(ctx'fordPaths)

  mut i uintptr = 0

  while i < len
    mut path = cast<ptr uint8>(val list:listGet(ctx'fordPaths, i))
    mut joinedPath = file:pathJoin(path, fordName)

    if file:pathExists(joinedPath)

      mut ret = parser:ctxParseFile(ctx, joinedPath, importAst)

      mem:free(joinedPath)
      mem:free(fordName)
      return ret
    /if

    mem:free(joinedPath)

    i++
  /while

  mut errors = list:listNew()

  mut toGlue [4] ptr uint8

  toGlue[0] = "Can't find "
  toGlue[1] = importName'name
  toGlue[2] = " in the given FORDPATHS"
  toGlue[3] = null

  mut glued = text:strglue(ptr toGlue[0])

  list:listAppend(errors, parser:issueNew(parser:ISSUE_ERR, importName'begin, ctx'currentFile, glued))

  mem:free(fordName)
  mem:free(glued)

  return errors
/func


func ctxImport(ctx ptr parser:Context, importName ptr ast:StringDef, value ptr ptr ast:PNode) ptr list:List

  if !map:mapGet(ctx'imports, importName, value)
    mut errors = findImport(ctx, importName, value)
    if list:listLen(errors) == 0
      map:mapPut(ctx'imports, text:strclone(importName'name), val value, map:FREE_KEY | map:FREE_VALUE)
    /if

    return errors
  else
    return list:listNew()
  /if

/func


func ctxImportAll(ctx ptr parser:Context, imports ptr list:List) ptr list:List
  mut len = list:listLen(imports)
  mut i uintptr = 0

  while i < len
    mut value ptr ast:PNode
    mut importName = cast<ptr ast:StringDef>(val list:listGet(imports, i))
    mut errs = ctxImport(ctx, importName, ptr value)

    if list:listLen(errs) != 0
      return errs
    /if

    i++
  /while

  return list:listNew()
/func


func envExtractFordPaths() ptr list:List
  mut list = list:listNew()

  mut paths = proc:getenv("FORDPATHS")
  if paths != null
    mut tkz = text:tokenizerNew(paths, 58) #`:`
    mut path ptr uint8

    while (path = text:tokenizerNext(tkz)) != null
      list:listAppend(list, path)
    /while

  /if

  return list
/func


func ctxGetImport(ctx ptr parser:Context, name ptr uint8) ptr ast:PRoot
  mut ret ptr ast:PRoot = null

  map:mapGet(ctx'imports, name, ptr ret)

  return ret
/func


func ctxFree(ctx ptr parser:Context)
  if ctx != null
    if ctx'imports != null
      map:mapFreeSpec(ctx'imports, ptr mem:free, cast<ptr func(item data)>(ptr ast:pnodeFree))
    /if

    if ctx'fordPaths != null
      list:listFreeAll(ctx'fordPaths, ptr mem:free)
    /if

    map:mapFreeSpec(ctx'builtins, null, cast<ptr func(item data)>(ptr ast:typeFree))

    mem:free(ctx'currentFile)
    mem:free(ctx)
  /if
/func


func ctxSetCurrentFile(ctx ptr parser:Context, filename ptr uint8)
  mem:free(ctx'currentFile)

  ctx'currentFile = text:strclone(filename)
/func


func ctxNew() ptr parser:Context
  mut ctx = cast<ptr parser:Context>(mem:zalloc(size(parser:Context)))
  ctx'imports = map:strmapNew()

  ctx'fordPaths = envExtractFordPaths()
  ctx'builtins = astfn:builtinSetNew()
  return ctx
/func


func ctxMatchBuiltin(ctx ptr parser:Context, name ptr uint8) ptr ast:Type
  mut value ptr ast:Type = null

  map:mapGet(ctx'builtins, name, ptr value)
  return value
/func


func ctxParseFile(ctx ptr parser:Context, fileName ptr uint8, newRoot ptr ptr ast:PNode) ptr list:List
  mut parser = synt:parserNew(fileName)

  val newRoot = synt:parserParse(parser)

  mut error = parser'err

  synt:parserFree(parser)

  if val newRoot == null
    mut list = list:listNew()
    list:listAppend(list, error)

    return list
  /if

  return sema:semaValidateAst(ctx, val newRoot)
/func
