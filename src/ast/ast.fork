#  Second Step - Experimental Fork Compiler
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2014
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.

import ast
import lex
import spring
import utils

func pnodeAppendChild(pnode, child ptr ast:PNode) ptr ast:PNode

  if pnode != null and child != null
    spring:listAppend(pnode'leaves, child)

    child'parent = pnode
  /if

  return pnode
/func


func pnodeFree(pnode ptr ast:PNode)
  if pnode != null
    if ast:isPRoot(pnode)

      mut proot = cast<ptr ast:PRoot>(pnode)
      ast:strdefFree(proot'moduleName)
      spring:free(proot'fileName)
      spring:listFreeAll(proot'imports, cast<ptr func(inst data)>(ptr ast:strdefFree))
      ast:stabFree(proot'aliases)

      utils:fpFree(proot'typePool)
    /if

    if ast:isPDecl(pnode)

      mut pdecl = cast<ptr ast:PDecl>(pnode)
      utils:decRef(pdecl'sym)

    /if

    if ast:isPBlock(pnode)
      ast:stabFree(cast<ptr ast:PBlock>(pnode)'syms)
    /if

    # remember to fix PBLOCK leaks.

    # remember to fix PEXPR leaks.

    spring:listFreeAll(pnode'leaves, ptr pnodeFree)

    spring:free(pnode)
  /if
/func


func pnodeNew(type uint16, value uintptr) ptr ast:PNode

  mut pnode = cast<ptr ast:PNode>(spring:zalloc(size(ast:PNode)))
  pnode'type = type
  pnode'value = value
  pnode'leaves = spring:listNew()

  return pnode
/func


func pblockNew() ptr ast:PBlock
  mut pblock = cast<ptr ast:PBlock>(spring:zalloc(size(ast:PBlock)))
  mut pnode = cast<ptr ast:PNode>(pblock)

  pnode'type = ast:PBLOCK
  pnode'leaves = spring:listNew()

  return pblock
/func


func pdeclNew(nodeType uint16, sym ptr ast:PSymbol) ptr ast:PDecl

  mut pdecl = cast<ptr ast:PDecl>(spring:zalloc(size(ast:PDecl)))
  pdecl'node'type = nodeType
  pdecl'node'leaves = spring:listNew()

  utils:incRef(sym)

  pdecl'sym = sym

  return pdecl
/func


func pexprTernaryNew(tok ptr lex:Token, lhs,mid,rhs ptr ast:PExpr, begin,end utils:Line) ptr ast:PExpr
    mut pexpr = cast<ptr ast:PExpr>(spring:zalloc(size(ast:PExpr)))
    pexpr'node'type = ast:PEXPR
    pexpr'node'value = tok'value
    pexpr'node'leaves = spring:listNew()
    pexpr'opType = tok'type
    tok'value = 0 # now the value belongs to this PExpr

    mut pnode = ptr pexpr'node
    pnode'begin = begin
    pnode'end = end

    ast:pnodeAppendChild(pnode, ptr lhs'node)
    ast:pnodeAppendChild(pnode, ptr mid'node)
    ast:pnodeAppendChild(pnode, ptr rhs'node)

    lex:tokenFree(tok)

    return pexpr
/func


func pexprNew(tok ptr lex:Token, lhs,rhs ptr ast:PExpr, begin,end utils:Line) ptr ast:PExpr
  return pexprTernaryNew(tok, lhs, null, rhs, begin, end)
/func


func pexprNewSingle(tok ptr lex:Token) ptr ast:PExpr
  return pexprNew(tok, null, null, lex:tokExtractBegin(tok), lex:tokExtractEnd(tok))
/func


func prootNew(moduleName ptr ast:StringDef, imports ptr spring:List, fileName ptr uint8) ptr ast:PRoot

  mut proot = cast<ptr ast:PRoot>(spring:zalloc(size(ast:PRoot)))
  mut pnode = cast<ptr ast:PNode>(proot)

  pnode'type = ast:PROOT
  pnode'leaves = spring:listNew()
  pnode'parent = null

  proot'moduleName = moduleName
  proot'imports = imports
  proot'fileName = spring:strclone(fileName)

  return proot

/func


func prootHasImported(root ptr ast:PRoot, name ptr uint8) bool
  mut i uintptr = 0
  mut len = spring:listLen(root'imports)

  while i < len
    mut elem = cast<ptr ast:StringDef>(val spring:listGet(root'imports, i))

    if spring:strequals(elem'name, name)
      return true
    /if

    i++
  /while

  return false
/func


func isPDecl(pnode ptr ast:PNode) bool
  return pnode != null and pnode'type == ast:PALIAS or pnode'type == ast:PDECL or pnode'type == ast:PMUT or pnode'type == ast:PENTRY or pnode'type == ast:PFUNCTION
/func

func isPBlock(pnode ptr ast:PNode) bool
  return pnode != null and (pnode'type == ast:PBLOCK or ast:isPRoot(pnode))
/func

func isPExpr(pnode ptr ast:PNode) bool
  return pnode != null and pnode'type == ast:PEXPR
/func


func isPRoot(pnode ptr ast:PNode) bool
  return pnode != null and pnode'type == ast:PROOT
/func

func strdefFree(strdef ptr ast:StringDef)
  if strdef != null
    spring:free(strdef'name)
    spring:free(strdef)
  /if
/func


func strdefNew(name ptr uint8, begin,end utils:Line) ptr ast:StringDef
  mut strdef = cast<ptr ast:StringDef>(spring:zalloc(size(ast:StringDef)))
  strdef'name = name
  strdef'begin = begin
  strdef'end = end

  return strdef
/func


mut PROOT      uint16 = 10
mut PENTRY     uint16 = 20
mut PFUNCTION  uint16 = 30
mut PALIAS     uint16 = 40
mut PDECL      uint16 = 50
mut PMUT       uint16 = 60
mut PIF        uint16 = 70
mut PEXPR      uint16 = 80
mut PBREAK     uint16 = 90
mut PCONTINUE  uint16 = 100
mut PRETURN    uint16 = 110
mut PIFELSE    uint16 = 120
mut PWHILE     uint16 = 130
mut PBLOCK     uint16 = 140
