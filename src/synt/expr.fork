#  libfork, a base library for the Fork language
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2014, 2015
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.


module synt


import ast
import astfn
import lex
import mem
import synt
import tokens
import utils


mut PTYPE_BINARY uint8 = 0
mut PTYPE_UNARY uint8 = 2

mut ASSOC_LEFT uint8 = 0
mut ASSOC_RIGHT uint8 = 1


func isBinary(tok ptr lex:Token) bool
  return tok? and (tok'type mod 10) < 2
/func


func isUnary(tok ptr lex:Token) bool
  return tok? and not isBinary(tok)
/func


func assocRight(tok ptr lex:Token) bool
  return tok? and ((tok'type & 1) != 0)
/func


func assocLeft(tok ptr lex:Token) bool
  return tok? and not assocRight(tok)
/func


func tokTypePrec(ttype uint16) int16
  if ttype == tokens:MINUS
    return tokTypePrec(tokens:PLUS)
  /if

  return ttype / 1000
/func


func opPrec(tok ptr lex:Token) int16
  if tok?
    return tokTypePrec(tok'type)
  /if

  return 0
/func


func isOp(tok ptr lex:Token) bool
  return opPrec(tok) != 0
/func


func isValidInExpr(tok ptr lex:Token) bool
  return tok? and (isOp(tok) or tok'type == tokens:ID or tok'type == tokens:NUMBER)
/func


method synt:Parser.parseCastTag() ptr ast:PExpr

  mut castTok ptr lex:Token = null

  # If we have the "cast" operator, than we should read it, and use it to mark
  # the expression's beginning.
  # Otherwise it's a `<` and we must create a "fake" cast token - to create a
  # cast PExpr.
  if me'next'type == tokens:CAST
    castTok = me.next()
  /if

  mut minor = me.expect(tokens:MINOR)

  if not minor?
    # tokenFree SHOULD check for nulls - like every free function should - and
    # be noop for them.
    lex:tokenFree(castTok)
    return null
  /if

  if not castTok?
    castTok = lex:tokenNew(tokens:CAST, 0, lex:linenoClone(minor'lineno), minor'endPos)
  /if

  lex:tokenFree(minor)

  mut type = me.parseType()
  if not type?
    lex:tokenFree(castTok)
    return null
  /if

  if not me.expectDiscard(tokens:MAJOR) or not me.expectDiscard(tokens:OPAR)
    lex:tokenFree(castTok)
    mem:decRef(type)
    return null
  /if

  mut expr = me.parseExpr()
  if not expr?
    lex:tokenFree(castTok)
    mem:decRef(type)
  /if

  mut cpar = me.expect(tokens:CPAR)
  if not cpar?
    lex:tokenFree(castTok)
    mem:decRef(type)
    return null
  /if

  mut pexpr = astfn:pexprNew(castTok, expr, null, lex:tokExtractBegin(castTok), lex:tokDiscardEnd(cpar))
  pexpr'node'value = <uintptr>(type)

  return pexpr
/method


method synt:Parser.parseSizeOp() ptr ast:PExpr

  mut sizeTok = me.next()


  if not me.expectDiscard(tokens:OPAR)
    lex:tokenFree(sizeTok)
    return null
  /if

  mut type = me.parseType()
  if not type?
    lex:tokenFree(sizeTok)
    return null
  /if

  mut cpar = me.expect(tokens:CPAR)
  if not cpar?
    lex:tokenFree(sizeTok)
    return null
  /if

  mut sizeop = astfn:pexprNew(sizeTok, null, null, lex:tokExtractBegin(sizeTok), lex:tokDiscardEnd(cpar))
  sizeop'node'value = <uintptr>(type)

  return sizeop
/method


func tokIsPostfixExpressionSuffixStart(tok ptr lex:Token) bool
  return tok? and   (tok'type == tokens:OPAR or tok'type == tokens:OBRAC \
                  or tok'type == tokens:INC or tok'type == tokens:DEC \
                  or tok'type == tokens:APOS or tok'type == tokens:DOT)
/func


method synt:Parser.parseId() ptr ast:PExpr
  mut first = me.next()
  mut ret = astfn:pexprNewSingle(first)

  if me'next? and me'next'type == tokens:COLON
    mut colon = me.next()

    mut id = me.expect(tokens:ID)
    if not id?
      lex:tokenFree(colon)
      ast:pnodeFree(<ptr ast:PNode>(ret))
      return null
    /if

    mut idExpr = astfn:pexprNewSingle(id)
    ret = astfn:pexprNew(colon, ret, idExpr, ret'node'begin, idExpr'node'end)
  /if

  return ret

/method


method synt:Parser.parseGenParens(cpar uint16) ptr ast:PExpr

  mut expression = me.parseAssignExpr()

  if not expression?
    return null
  /if

  mut cparTok = me.expect(cpar)
  if not cparTok?
    ast:pnodeFree(<ptr ast:PNode>(expression))
    return null
  /if

  lex:tokenFree(cparTok)

  return expression

/method


method synt:Parser.parseParens() ptr ast:PExpr

  me.discardNext()
  return me.parseGenParens(tokens:CPAR)

/method


method synt:Parser.parseExprList(parent ptr ast:PExpr,
                         endToken uint16) ptr ast:PExpr

  mut parentNode = <ptr ast:PNode>(parent)

  while me'next? and me'next'type != endToken
    mut expr = me.parseAssignExpr()

    if not expr?
      ast:pnodeFree(parentNode)

      return null
    /if

    astfn:pnodeAppendChild(parentNode, <ptr ast:PNode>(expr))

    if not me'next? or not (me'next'type == tokens:COMMA or me'next'type == endToken)
      me'err = lex:errorMism(me'last, "a comma ','", lex:tokenStr(me'next))
      ast:pnodeFree(parentNode)

      return null
    /if

    if me'next'type == endToken
      break
    /if

    me.discardNext() # discard the comma
  /while

  mut end = me.expect(endToken)
  if not end?
    ast:pnodeFree(parentNode)

    return null
  /if

  parent'node'end = lex:tokDiscardEnd(end)

  return parent
/method


method synt:Parser.parseCurly() ptr ast:PExpr
  mut ocurly = me.next()

  mut ret = me.parseExprList(astfn:pexprNewSingle(ocurly), tokens:CCURLY)

  return ret
/method


method synt:Parser.parsePrimaryExpr() ptr ast:PExpr

  if not me'next?
    me'err = lex:error(me'last, "expected a primary expression, got an EOF")
    return null
  /if

  if    me'next'type == tokens:FALSE \
     or me'next'type == tokens:TRUE \
     or me'next'type == tokens:STRING \
     or me'next'type == tokens:NUMBER \
     or me'next'type == tokens:NULL \
     or me'next'type == tokens:ME

    return astfn:pexprNewSingle(me.next())
  /if

  if me'next'type == tokens:ID
    return me.parseId()
  /if

  if me'next'type == tokens:CAST or me'next'type == tokens:MINOR
    return me.parseCastTag()
  /if

  if me'next'type == tokens:SIZE
    return me.parseSizeOp()
  /if

  if me'next'type == tokens:OCURLY
    return me.parseCurly()
  /if

  if me'next'type != tokens:OPAR
    me'err = lex:errorMism(me'next'lineno, "an identifier, a costant, a struct literal or an expression between ()", lex:tokenStr(me'next))
    return null
  /if

  return me.parseParens()

/method


method synt:Parser.parseAccessExpr(lhs ptr ast:PExpr) ptr ast:PExpr

  mut obrac = me.next()
  mut expression = me.parseGenParens(tokens:CBRAC)

  if not expression?
      return null
  /if

  return astfn:pexprNew(obrac, lhs, expression, lhs'node'begin, expression'node'end)

/method


method synt:Parser.parseCallExpr(lhs ptr ast:PExpr) ptr ast:PExpr

  mut opar = me.next()

  if not opar?
    return null
  /if

  mut begin = lex:tokExtractBegin(opar)

  return me.parseExprList(astfn:pexprNew(opar, lhs, null, begin, begin), tokens:CPAR)
/method


method synt:Parser.parseFieldDotExpr(lhs ptr ast:PExpr) ptr ast:PExpr

  # APOS or DOT
  mut op = me.next()
  mut id = me.expect(tokens:ID)

  if not id?
    lex:tokenFree(op)
    return null
  /if

  mut idExpr = astfn:pexprNewSingle(id)

  return astfn:pexprNew(op, lhs, idExpr, lhs'node'begin, idExpr'node'end)
/method


method synt:Parser.parsePostfixSuffix(primaryLhs ptr ast:PExpr) ptr ast:PExpr

  mut lhs = primaryLhs

  while true
    if not me'next?
      break
    /if

    if me'next'type == tokens:OBRAC
      lhs = me.parseAccessExpr(lhs)
    else
      if me'next'type == tokens:OPAR
        lhs = me.parseCallExpr(lhs)
      else
        if me'next'type == tokens:INC or me'next'type == tokens:DEC or me'next'type == tokens:QUEST
          mut pfOp = me.next()
          lhs = astfn:pexprNew(pfOp, lhs, null, lhs'node'begin, lex:tokExtractEnd(pfOp))
        else
          if me'next'type == tokens:APOS or me'next'type == tokens:DOT
            lhs = me.parseFieldDotExpr(lhs)
          else
            break
          /if
        /if
      /if
    /if

    if not lhs?
      return null
    /if
  /while

  return lhs
/method


method synt:Parser.parsePostfixExpr() ptr ast:PExpr
  mut primary = me.parsePrimaryExpr()

  if not primary?
    return null
  /if

  return me.parsePostfixSuffix(primary)
/method


method synt:Parser.parseUnaryExprOp() ptr ast:PExpr
  mut operator = me.next()

  mut result = me.parseUnaryExpr()
  if not result?
    lex:tokenFree(operator)
    return null
  /if

  if operator'type == tokens:MINUS
    operator'type = tokens:UMINUS
  /if

  return astfn:pexprNew(operator, result, null, lex:tokExtractBegin(operator), result'node'end)
/method


method synt:Parser.parseUnaryExpr() ptr ast:PExpr
  if not me'next?
    me'err = lex:error(me'last, "unexpected EOF")
    return null
  /if

  if isUnary(me'next)
    return me.parseUnaryExprOp()
  /if

  return me.parsePostfixExpr()
/method


func isConditionalOperator(tok ptr lex:Token) bool
  return tok? and tok'type == tokens:FATARROW
/func


method synt:Parser.parseBinaryRhs(lhs ptr ast:PExpr, minPri uint16) ptr ast:PExpr
  mut curOpPrec uint16
  mut rhs ptr ast:PExpr = null

  while me'next? and ((curOpPrec = opPrec(me'next)) >= minPri)
    mut opTok = me.next()
    mut opIsRight = assocRight(opTok)

    mut ternaryMiddle ptr ast:PExpr = null

    if isConditionalOperator(opTok)
      ternaryMiddle = me.parseExpr()
      if not ternaryMiddle? or not me.expectDiscard(tokens:SEPARATOR)
        lex:tokenFree(opTok)
        return null
      /if
    /if

    rhs = me.parseUnaryExpr()

    if not rhs?
      return null
    /if

    mut nextOpPrec = opPrec(me'next)

    if me'next? and (nextOpPrec > curOpPrec or (opIsRight and nextOpPrec == curOpPrec))
      mut rhsMinPrec = curOpPrec
      if not opIsRight
        curOpPrec++
      /if

      rhs = me.parseBinaryRhs(rhs, curOpPrec)
      if not rhs?
        return null
      /if
    /if

    lhs = astfn:pexprNewTernary(opTok, lhs, ternaryMiddle, rhs, lhs'node'begin, rhs'node'end)
  /while

  return lhs
/method


method synt:Parser.parseAssignExpr() ptr ast:PExpr
  mut lhs = me.parseUnaryExpr()

  return lhs? => me.parseBinaryRhs(lhs, tokTypePrec(tokens:ASSIGN)) || null
/method


method synt:Parser.parseExpr() ptr ast:PExpr
  return me.parseAssignExpr()
/method
