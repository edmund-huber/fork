#  libfork, a base library for the Fork language
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2014, 2015
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.


import list
import mem
import tty


func listSeekAhead(list ptr list:List, pos uintptr)

  if (list'current == null) or (pos == 0)
    list'current = list'start
    list'pos = 0
  /if

  while list'pos < pos
    list'current = list'current'next
    list'pos++
  /while

/func


func listSeekBehind(list ptr list:List, pos uintptr)

  if (list'current == null) or ((list'len - 1) == pos)
    list'current = list'end
    list'pos = list'len - 1
  /if

  while list'pos > pos
    list'current = list'current'prec
    list'pos--
  /while

/func


func listSeekZero(list ptr list:List)
  list'current = list'start
  list'pos = 0
/func


func listSeek(list ptr list:List, pos uintptr)
  if (list'current == null) and (pos == 0)
     list:listSeekZero(list)
  /if

  if pos > list'pos
    listSeekAhead(list, pos)
  else
    if pos < list'pos
      listSeekBehind(list, pos)
    /if
  /if
/func


func listPushint(list ptr list:List, value uintptr)
  mut new = cast<ptr list:Lnode>(mem:zalloc(size(list:Lnode)))

  new'next = list'start
  if list'start != null
    list'start'prec = new
  /if

  if list'end == null
    list'end = new
    list'current = new
  else
    list'pos++
  /if

  new'value = value
  list'start = new

  list'len++
/func


mut listPush = cast<ptr func(list ptr list:List, item data)>(ptr listPushint)

func listAppendint(list ptr list:List, value uintptr) intptr
  if list'start == null
    listPushint(list, value)
    return 1
  /if

  mut new = cast<ptr list:Lnode>(mem:zalloc(size(list:Lnode)))

  new'value = value
  new'prec = list'end
  list'end'next = new

  list'end = new

  return cast<intptr>(list'len++)
/func


mut listAppend = cast<ptr func(list ptr list:List, item data) intptr>(ptr listAppendint)

func listAddint(list ptr list:List, pos uintptr, value uintptr) intptr
  if pos > list'len
    return -1
  /if

  if pos == list'len
    return listAppendint(list, value)
  /if

  listSeek(list, pos)

  mut new = cast<ptr list:Lnode>(mem:zalloc(size(list:Lnode)))
  mut prec = list'current'prec

  if prec != null
    prec'next = new
  /if

  new'prec = prec
  new'value = value
  new'next = list'current
  list'current'prec = new

  list'current = cast<ptr list:Lnode>(list'pos = 0)

  list'len++

  return cast<intptr>(pos)
/func


mut listAdd = cast<ptr func(list ptr list:List, index uintptr, item data) intptr>(ptr listAddint)

func listInit(list ptr list:List) ptr list:List
  mem:memset(list, 0, size(list:List))
  return list
/func


func listNew() ptr list:List
  return list:listInit(cast<ptr list:List>(mem:alloc(size(list:List))))
/func


func listExtract(list ptr list:List, start uintptr, len intptr) ptr list:List

  mut extract = listNew()

  if start < list'len
    if len > list'len
      len = cast<intptr>(list'len - start)
    /if

    if len < 0
      len = cast<intptr>(list'len - start)
    /if
    listSeek(list, start)

    mut prec = list'current'prec
    mut link1 ptr ptr list:Lnode
    if prec != null
      link1 = ptr prec'next
    else
      link1 = ptr list'start
    /if

    extract'current = extract'start = extract'end = list'current

    listSeek(list, start + len - 1)

    mut next = list'current'next
    mut link2 ptr ptr list:Lnode
    if next != null
      link2 = ptr next'prec
    else
      link2 = ptr list'end
    /if

    extract'end = list'current

    extract'len = len

    val link1 = next
    val link2 = prec

    extract'start'prec = extract'end'next = null

    list'len = list'len - len

    list'current = list'start
    list'pos = 0
  /if

  return extract

/func


func lnodeFree(lnode ptr list:Lnode)
  while lnode != null
    mut next = lnode'next
    mem:free(lnode)
    lnode = next
  /while
/func


func listDeinit(list ptr list:List)
  lnodeFree(list'start)
/func


func listFree(list ptr list:List)
  if list != null
    list:listDeinit(list)

    mem:free(list)
  /if
/func


func lnodeFreeAll(lnode ptr list:Lnode, freefunc ptr func(item data))
  while lnode != null
    mut next = lnode'next
    freefunc(cast<data>(lnode'value))
    mem:free(lnode)
    lnode = next
  /while
/func


func listDeinitAll(list ptr list:List, freefunc ptr func(item data))
  lnodeFreeAll(list'start, freefunc)
/func


func listFreeAll(list ptr list:List, freefunc ptr func(item data))
  list:listDeinitAll(list, freefunc)
  mem:free(list)
/func


func lnodeFreeContents(lnode ptr list:Lnode, freefunc ptr func(item data))
  while lnode != null
    mut next = lnode'next
    freefunc(cast<data>(lnode'value))
    lnode = next
  /while
/func


func listFreeContents(list ptr list:List, freefunc ptr func(item data))
  lnodeFreeContents(list'start, freefunc)
/func


func listGetint(list ptr list:List, pos uintptr) ptr uintptr
  if pos >= list'len
    return null
  /if

  listSeek(list, pos)

  if list'current != null
    return ptr list'current'value
  /if

  return null
/func


mut listGet = cast<ptr func(list ptr list:List, index uintptr) ptr data>(ptr listGetint)

func listInsertint(list ptr list:List, pos uintptr, value uintptr) intptr
  if pos > list'len
    return -1
  /if

  if pos == list'len
    return listAppendint(list, value)
  /if

  listSeek(list, pos)

  list'current'value = value

  return cast<intptr>(list'pos)
/func


mut listInsert = cast<ptr func(list ptr list:List, index uintptr, item data) intptr>(ptr listInsertint)

func listLen(list ptr list:List) uintptr
  return list'len
/func


func listPopint(list ptr list:List) uintptr
  if list'start == null
    return 0
  /if

  mut next = list'start'next
  mut ret = list'start'value

  mem:free(list'start)

  if next != null
    next'prec = null
  /if

  if list'start == list'end
    mem:memset(list, 0, size(list:List))
  else
    list'start = next
    list'len--
  /if

  return ret
/func


mut listPop = cast<ptr func(list ptr list:List) data>(ptr listPopint)

func listPrune(list ptr list:List)
  lnodeFree(list'start)
  mem:memset(list, 0, size(list:List))
/func


func listShallowCopy(list ptr list:List) ptr list:List
  mut new = listNew()
  mut len = listLen(list)
  mut i uintptr = 0

  while i < len
    listAppendint(new, val listGetint(list, i))
    i++
  /while

  return new
/func


func listPrint(list ptr list:List)
  mut inside = false
  tty:out("[ ")
  mut len = listLen(list)
  mut i uint8 = 0

  while i < len
    if inside
      tty:out(", ")
    else
      inside = true
    /if

    tty:outint(val listGetint(list, i))
    i++
  /while
  tty:outln(" ]")
/func



func listRemove(list ptr list:List, item uintptr, freefunc ptr func(item data)) bool
  mut len = list:listLen(list)

  if item >= len
    return false
  /if

  listSeek(list, item)

  mut prec = list'current'prec
  mut next = list'current'next

  mut precNodeNext = ptr list'start

  if prec != null
    precNodeNext = ptr prec'next
  /if

  mut nextNodePrec = ptr list'end

  if next != null
    nextNodePrec = ptr next'prec
  /if

  val precNodeNext = next
  val nextNodePrec = prec

  if freefunc != null
    freefunc(cast<data>(list'current'value))
  /if

  mem:free(list'current)

  list'len--

  if item == 0
    list'current = next
    # position is 0 already
  else
    list'current = prec
    list'pos--
  /if

  return true
/func
