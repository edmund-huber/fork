#  ctrans, Fork to C transpiler
#  Copyright (C) Marco Cilloni <marco.cilloni@yahoo.com> 2015
#
#  This Source Code Form is subject to the terms of the Mozilla Public
#  License, v. 2.0. If a copy of the MPL was not distributed with this
#  file, You can obtain one at http://mozilla.org/MPL/2.0/.
#  Exhibit B is not attached; this software is compatible with the
#  licenses expressed under Section 1.12 of the MPL v2.


import ast
import ctrans
import list
import mem
import parser
import proc
import tty
import txt


entry
  mut argv = proc:args()
  if argv'len != 1
    tty:errln("error: wrong number of arguments, required: 1")
    proc:exit(1)
  /if

  mut prs = parser:new()
  mut fname = txt:strequals(argv'args[0], "-") => "/dev/stdin" || argv'args[0]
  mut {ast,issues} = prs.parseFile(fname)

  mut isFord = txt:strEndsWith(fname, ".ford")

  mut errLen = issues.len()

  if errLen > 0
    mut i uintptr = 0

    while i < errLen
      mut issue = issues.get(i)

      issue.writeOut(ptr tty:err)

      i++
    /while

    ast:issuesFree(issues)
    proc:exit(1)
  /if

  mut root = <ptr ast:PRoot>(ast)

  mut tr ctrans:Transl

  ctrans:init(ptr tr, isFord, root'moduleName? => root'moduleName'name || null)

  tr.translate(root)

  mut ttys = tty:ttyStream()

  tr.dump(ptr ttys)

  parser:free(prs)

  ast:pnodeFree(ast)
/entry
